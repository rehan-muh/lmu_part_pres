clear()

##########################
#Reading-in Sequence Data#
###########################


#Put the path to the folder containing ringe_modified.nex on your machine here
path = "/Users/loaner/Desktop/RevBayes/data/"

filenames <- v(path + "ringelexicalcharacters.nex", path + "ringephonologicalcharacters.nex", path + "ringemorphologicalcharacters.nex")

n_data_subsets <- filenames.size()

### Read in sequence data
for (i in 1:n_data_subsets){
    data[i] = readDiscreteCharacterData(filenames[i])
    num_sites[i] = data[i].nchar()
}
taxa <- data[1].taxa()
n_taxa <- data[1].ntaxa()
n_branches <- 2 * n_taxa - 3

#############################
#Setting up Moves and Monitors#
#############################

moves    = VectorMoves()
monitors = VectorMonitors()

anatolian = clade("HI", "LY", "LU")

####################
#Substitution Model#
####################

n_pi_cats = 4

# Define F81 parameters for each dataset
for (i in 1:n_data_subsets) {
    pi_alpha[i] ~ dnExponential(1.0)
    moves.append(mvScale(pi_alpha[i]))

    pis[i] := fnDiscretizeBeta(pi_alpha[i], pi_alpha[i], n_pi_cats)

    for (j in 1:n_pi_cats) {
        pi[i][j] := simplex([abs(pis[i][j]), abs(1.0 - pis[i][j])])
        Q[i][j] := fnF81(pi[i][j])
    }

    site_matrices[i] <- simplex(rep(1, n_pi_cats))
}

####################
#Site-rate model#
###################

source("/Users/loaner/Desktop/RevBayes/scripts/mcmc/site_variation.Rev") #site-rate model

##########################################################################################
# Tree Model: Simple #
##########################################################################################


topology ~ dnUniformTopology(taxa, outgroup=anatolian)


# These are moves that change the tree topology
moves.append( mvNNI(topology, weight=n_taxa/2.0) )
moves.append( mvSPR(topology, weight=n_taxa/10.0) )

# Branch length prior
for (i in 1:n_branches) {
    bl[i] ~ dnExponential(10.0)
    moves.append( mvScale(bl[i]) )
}

TL := sum(bl)

psi := treeAssembly(topology, bl)

###################
# PhyloCTMC Model #
###################

for (i in 1:n_data_subsets) {
    seq[i] ~ dnPhyloCTMC(tree=psi, Q=Q[i], branchRates=part_rate[i], siteRates=gamma_rates[i], pInv=pinvar[i], type="Standard", siteMatrices=site_matrices[i])
    seq[i].clamp(data[i])
}
############
# Analysis #
############

#You can use any node as the argument of model()
mymodel = model(psi)

####################################
# Run the power-posterior analysis #
####################################

pow_p = powerPosterior(mymodel, moves, monitors, "output/ringe_f81_part_comp_powp.out", cats=127, sampleFreq=10)
#pow_p.burnin(generations=2000,tuningInterval=200)
pow_p.run(generations=10000)

###################################
# Run the stepping-stone analysis #
###################################

ss = steppingStoneSampler(file="output/ringe_f81_part_comp_powp.out", powerColumnName="power", likelihoodColumnName="likelihood")

write("Stepping stone marginal likelihood:\t", ss.marginal() )

ps = pathSampler(file="output/ringe_f81_part_comp_powp.out", powerColumnName="power", likelihoodColumnName="likelihood")
write("Path-sampling marginal likelihood:\t", ps.marginal() )
